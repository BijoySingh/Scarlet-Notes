apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.appconfig_compile_sdk_version

    defaultConfig {
        minSdkVersion rootProject.ext.appconfig_min_os_version
        targetSdkVersion rootProject.ext.appconfig_target_os_version
        versionCode rootProject.ext.appconfig_version_code
        versionName rootProject.ext.appconfig_version

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resValue "string", "file_provider_name", "com.maubis.scarlet.pro.base.support.GenericFileProvider"
    }

    buildTypes {
        release {
            minifyEnabled false
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    api project(':markdown')

    api 'androidx.recyclerview:recyclerview:1.1.0'
    api 'androidx.cardview:cardview:1.0.0'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'androidx.appcompat:appcompat:1.1.0'
    api 'androidx.constraintlayout:constraintlayout:1.1.3'

    api 'com.google.android.material:material:1.0.0'

    api 'com.google.code.gson:gson:2.8.5'

    api "com.github.bijoysingh:android-basics:5.1.0-x"
    api "com.github.bijoysingh:ui-basics:1.0.0-x"
    api 'com.github.bijoysingh:floating-bubble:3.0.0'

    api 'com.evernote:android-job:1.2.6'

    implementation 'com.google.android:flexbox:0.3.2'

    api 'androidx.room:room-runtime:2.2.2'
    api 'androidx.room:room-testing:2.2.2'
    kapt 'androidx.room:room-compiler:2.2.2'

    def biometric_version = "1.0.0"
    implementation "androidx.biometric:biometric:$biometric_version"

    implementation 'com.github.jkwiecien:EasyImage:1.3.1'

    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1"

    def litho_version = "0.31.0"
    api "com.facebook.litho:litho-core:$litho_version"
    api "com.facebook.litho:litho-widget:$litho_version"
    compileOnly "com.facebook.litho:litho-annotations:$litho_version"
    kapt "com.facebook.litho:litho-processor:$litho_version"
    api 'com.facebook.soloader:soloader:0.5.1'

    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
}

apply plugin: 'kotlin-android-extensions'